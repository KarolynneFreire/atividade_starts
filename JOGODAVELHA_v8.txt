Algoritmo "jogodavelha"

Var
   linha, coluna, cont, l, opcao, chance, vez,bis,numrepetido, menu: inteiro
   joguinho: vetor [1..3,1..3] de caractere
   x : caractere
   vencedor :logico
   jogador1,jogador2, a:  caractere

   //------------------Procedimentos-----------------------------

procedimento mensagemInicial()
inicio
   escreval ("###########################")
   escreval("      JOGO DA VELHA ")
   escreval ("############################")
   escreval ()
   escreval ("O primeiro jogador que digitar o nome será [X],")
   escreval("já o segundo será [0].")
   escreval ()
   escreval ()
   escreval (" ### DESEJAMOS BOA SORTE! ### ")
   escreval ()
   escreva("Jogador(a) 01, digite seu nome: ")
   leia(nome1)
   escreva("Jogador(a) 02, digite seu nome: ")
   leia(nome2)

   limpatela

   escreval("Bem vindos(as) ", nome1, " e ", nome2)
   escreval("Que comecem os jogos!")
   escreval()

fimprocedimento
//-----------------------------------------------------
procedimento preencherJogo()
Inicio

   para linha de 1 ate 3 faca
      para coluna de 1 ate 3 faca
         cont <- cont +1
         joguinho[linha, coluna] <- numpcarac(cont)
      fimpara
   fimpara
fimprocedimento

//-----------------------------------------------------
procedimento estruturaJogo()
Inicio

   escreval ("####################")

   escreval("   JOGO DA VELHA ")
   escreval ("-------------------")
   para linha de 1 ate 3 faca
      para coluna de 1 ate 3 faca
         escreva("| ")
         escreva(joguinho[linha,coluna]:4)
      fimpara
      escreva ("| ")
      escreval ()
      escreval ("-------------------")
   fimpara
   escreval ("####################")
fimprocedimento
//-----------------------------------------------------
procedimento jogarJogo()
Inicio

   x <- "X"
   vencedor<- falso
   chance <- 0
   vez<- 1

   repita

      escreval(" Digite um número para ", x ,": " )
      leia(opcao)

      se (opcao >0) e (opcao <10) entao
         se (opcao >0) e (opcao <4) entao
            l <- 1
            se (joguinho[l,opcao] = "X") ou(joguinho[l,opcao] = "0") entao
               numeroRepetido()
            senao
               joguinho [l,opcao] <- x
               vezJogador()
            fimse
         senao
            se (opcao >3) e (opcao <7) entao
               l <- 2
               se (joguinho[l,opcao-3] = "X") ou(joguinho[l,opcao-3] = "0") entao
                  numeroRepetido()
               senao
                  joguinho [l,opcao-3] <- x
                  vezJogador()

               fimse
            senao
               l <- 3
               se (joguinho[l,opcao-6] = "X") ou(joguinho[l,opcao-6] = "0") entao
                  numeroRepetido()
               senao
                  joguinho [l,opcao-6] <- x
                  vezJogador()

               fimse
            fimse
         fimse
      senao
         escreval("Opção invalida" )
         leia(a)
      fimse
      limpatela
      estruturaJogo()

   ate (vencedor = verdadeiro) ou (vez = 9)
   se (vencedor = verdadeiro) entao
      Se X="X" entao
         escreva( jogador1 , " venceu!!!")
      senao
         escreva(jogador2 , " venceu!!!")
      fimse
   senao
      escreval ("XIII!",jogador1, " e ", jogador2," DEU VELHAAAA...")
   fimse

   jogarNovamente()

fimprocedimento
//-----------------------------------------------------
procedimento vezJogador()
Inicio

   vencedor <- verificarVencedor()
   chance <- chance + 1
   se (vencedor = falso) entao
      se (vez%2 <> 0) entao
         x <- "0"
      senao
         x <- "X"
      fimse
      vez <- vez +1
   fimse

fimprocedimento
//-----------------------------------------------------
procedimento numeroRepetido()
inicio
   escreval("Eita, esse número já foi digitado!")
   escreva("Aperte enter para continuar e preste mais atenção")
   leia(numrepetido)
fimprocedimento

//-----------------------------------------------------
procedimento jogarNovamente()
inicio
   repita
      escreval()
      escreval("Desejam jogar novamente?")
      escreval()

      escreval("[1] Sim")
      escreval("[2] Não")
      escreval()
      escreva("Opção escolhida: ")
      leia(bis)
      limpatela

      escolha bis
      caso 1
         cont <- 0
         preencherJogo()
         estruturaJogo()
         jogarJogo()
      caso 2
         escreval("Jogo encerrado")
      fimescolha
   ate(bis = 2)
fimprocedimento

//-----------------------------------------------------
procedimento historico()
inicio
   se (contP1 = 1) entao
      escreval("O jogador(a) ", jogador1," ganhou ", contP1, " vez!")
   senao
      escreval("O jogador(a) ", jogador1," ganhou ", contP1, " vezes!")
   fimse
   se (contp2 = 1) entao
      escreval("O jogador(a) ", jogador2," ganhou ", contP2, " vez!")
   senao
      escreval("O jogador(a) ", jogador2," ganhou ", contP2, " vezes!")
   fimse
fimProcedimento

//-------------------Funcão----------------------------
funcao verificarVencedor() : logico
var
   venceu : logico
Inicio
   venceu <- falso

   para linha de 1 ate 3  faca
      se ((joguinho[linha,1] = joguinho[linha,2] ) e (joguinho[linha,2] = joguinho[linha,3] )) entao
         venceu <- verdadeiro
      fimse
   fimpara

   para linha de 1 ate 3  faca
      se ((joguinho[1,linha] = joguinho[2,linha] ) e (joguinho[2,linha] = joguinho[3,linha] )) entao
         venceu <- verdadeiro
      fimse
   fimpara


   se ((joguinho[1,1] = joguinho[2,2] ) e (joguinho[2,2] = joguinho[3,3] )) entao
      venceu <- verdadeiro
   fimse

   se ((joguinho[1,3] = joguinho[2,2] ) e (joguinho[2,2] = joguinho[3,1] )) entao
      venceu <- verdadeiro
   fimse

   retorne venceu
fimfuncao

//-----------------------------------------------------
Inicio
   cont <- 0


   mensagemInicial()
   preencherJogo()
   estruturaJogo()
   jogarJogo()



Fimalgoritmo